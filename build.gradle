buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.2'
    }
}

plugins {
    id 'maven'
    id 'maven-publish'
    id 'net.saliman.cobertura' version '2.2.8'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id 'com.github.ksoichiro.console.reporter' version '0.1.3'
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'
apply from: "${rootDir}/gradle/meta.gradle"
apply from: "${rootDir}/gradle/release.gradle"

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testRuntime files(createClasspathManifest)
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntime 'cglib:cglib-nodep:3.2.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

cobertura {
    coverageFormats = ['html', 'xml']
}

idea {
    module {
        excludeDirs += [
            file('.repo'),
            file('samples/simple/.gradle'),
            file('samples/simple/build'),
            file('samples/jacoco/.gradle'),
            file('samples/jacoco/build'),
        ]
    }
}
